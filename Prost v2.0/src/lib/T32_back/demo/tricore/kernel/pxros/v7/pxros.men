; --------------------------------------------------------------------------------
; @Title: PXROS specific menu
; @Description: -
; @Author: DIE
; @Copyright: (c) 1989-2016 Lauterbach GmbH, licensed for use with TRACE32(R) only
; --------------------------------------------------------------------------------
; $Id: pxros.men 4656 2019-10-01 13:29:53Z rdienstbeck $

add
menu
(
  popup "&PXROS"
  (
    default
    menuitem "Display &Tasks"       "TASK.ListTask"
    menuitem "List all &Objects"    "TASK.ListObj.all"
    menuitem "List &Messages"       "TASK.ListObj.MeSsaGe"
    menuitem "List &Delays"         "TASK.ListObj.DeLaY"
    menuitem "List Memory &Classes" "TASK.ListObj.MemClass"
    menuitem "List Mail&boxes"      "TASK.ListObj.MailBoX"
    menuitem "List &Object Pools"   "TASK.ListObj.OPool"
    separator
    popup "&Stack Coverage"
    (
      menuitem "&List Stacks"    "TASK.STacK.view"
      menuitem "Add Task"        "TASK.STacK.ADD"
      menuitem "Remove Task"     "TASK.STacK.ReMove"
      menuitem "Reset Coverage"
      (
        if icd()||simulator()
        (
          dialog.yesno "Overwrite unused stack space" "with stack fill pattern?"
          entry &yes
          if !&yes
            enddo
        )
        TASK.STacK.Init
      )
    )
  )
  popup "Trace"
  (
    popup "[:alist]List"
    (
      separator
      menuitem "&Task Switches"     "Trace.List List.TASK"
      menuitem "&Default and Tasks" "Trace.List List.TASK DEFault"
    )
  )
  popup "Perf"
  (
    separator
    popup "&Task Runtime"
    (
      menuitem "&Prepare"
      (
        if t.method.analyzer()
        (
          Analyzer.AutoInit on
        )
        if ice()
        (
          Analyzer.ReProgram
          (
            Sample.Enable if AlphaBreak&&Write
          )
          Break.Delete /Alpha
          Break.Set task.config(magic)++(task.config(magicsize)-1) /Alpha
        )
        if fire()
        (
          Analyzer.ReProgram
          (
            Sample.Enable if AlphaBreak
          )
          Break.Delete /Alpha
          Break.Set task.config(magic)++(task.config(magicsize)-1) /Alpha /Write
        )
        if a.mode.flow()
        (
          Break.Delete /TraceEnable
          Break.Set task.config(magic) /TraceEnable
        )
      )
      menuitem "[:perf]Show &Numerical"   "Trace.STATistic.TASK"
      menuitem "[:achart]Show as &Timing" "Trace.CHART.TASK"
      menuitem "[:achart]Tracking with Trace &List"
      (
        Trace.List List.TASK DEFault /Track
        Trace.CHART.TASK /Track
      )
    )
    popup "Task &Function Runtime"
    (
      menuitem "&Prepare"
      (
        if t.method.analyzer()
        (
          Analyzer.AutoInit on
          Analyzer.STATistic.PreFetch on
        )

        if ice()
        (
          if a.config.hac()
          (
             Analyzer.ReProgram
             (
               Sample.Enable if AlphaBreak
               Sample.Enable if BetaBreak
               Mark.A        if AlphaBreak
               Mark.B        if BetaBreak
             )
          )
          else
          (
             Analyzer.ReProgram
             (
               Sample.Enable if AlphaBreak||BetaBreak
               Mark.A if AlphaBreak
               Mark.B if BetaBreak
             )
          )
          Break.Delete /Alpha /Beta /Charly
          Break.SetFunc
          Break.Set task.config(magic)++(task.config(magicsize)-1) /Alpha
        )
        if fire()
        (
          Analyzer.ReProgram
             (
               Sample.Enable if AlphaBreak||BetaBreak
               Mark.A if AlphaBreak
               Mark.B if BetaBreak
             )
          Break.Delete /Alpha /Beta /Charly
          Break.SetFunc
          Break.Set task.config(magic)++(task.config(magicsize)-1) /Alpha /write
        )
        if a.mode.flow()
        (
          Break.Delete /TraceData
          Break.Set task.config(magic) /TraceData
        )
      )
      menuitem "[:perf]Show &Numerical"     "Trace.STATistic.TASKFUNC"
      menuitem "[:perf]Show as &Tree"       "Trace.STATistic.TASKTREE"
      menuitem "[:perf]Show &Detailed Tree" "Trace.STATistic.TASKTREE ALL"
      menuitem "[:achart]Show as &Timing"   "Trace.CHART.TASKFUNC"
      menuitem "[:alist]Show N&esting"      "Trace.List List.TASK FUNC TI.FUNC"
    )
    popup "Task &Status"
    (
      menuitem "&Prepare"
      (
        if t.method.analyzer()
        (
          Analyzer.AutoInit on
        )
        if ice()
        (
          Analyzer.ReProgram
          (
            Sample.Enable if AlphaBreak&&Write
          )
          Break.Delete /Alpha
          Break.Set task.config(magic)++(task.config(magicsize)-1) /Alpha
          TASK.TASKState
        )
        if fire()
        (
          Analyzer.ReProgram
          (
            Sample.Enable if AlphaBreak
          )
          Break.Delete /Alpha
          Break.Set task.config(magic)++(task.config(magicsize)-1) /Alpha /Write
          TASK.TASKState
        )
      )
      menuitem "[:perf]Show &Numerical"   "Trace.STATistic.TASKSTATE"
      menuitem "[:achart]Show as &Timing" "Trace.CHART.TASKSTATE"
      menuitem "[:achart]Tracking with Trace &List"
      (
        Trace.List List.TASK DEFault /Track
        Trace.CHART.TASKSTATE /Track
      )
    )
    popup "CPU Load"
    (
        menuitem "Set Idle Task..."
        (
            dialog
            (
                header "Set Idle Task"
                pos 0. 0. 24. 3.
                box "Name of idle task"
                pos 1. 1. 22. 1.
idletask:       defedit "InitTask_C0" ""
                pos 3. 3. 7. 1.
                defbutton "OK"
                (
                    LOCAL &idletask
                    &idletask=dialog.string(idletask)
                    GROUP.CreateTASK "idle" "&idletask"
                    GROUP.COLOR "idle"  NONE
                    GROUP.COLOR "other" RED
                    dialog.end
                )
                pos 14. 3. 7. 1.
                button "Cancel" "dialog.end"
            )
        )
        menuitem "[:perf]Show Numerical"
        (
            IF !GROUP.EXIST(idle)
            (
                DIALOG.OK "Please set idle task first!"
                ENDDO
            )
            GROUP.MERGE "idle"
            GROUP.MERGE "other"
            Trace.STATistic.TASK
        )
        menuitem "[:aprochart]Show Profiling"
        (
            IF !GROUP.EXIST(idle)
            (
                DIALOG.OK "Please set idle task first!"
                ENDDO
            )
            GROUP.MERGE "idle"
            GROUP.MERGE "other"
            Trace.PROfileChart.TASK
        )
        menuitem "Disable CPU Load View"
        (
            GROUP.SEParate "idle"
            GROUP.SEParate "other"
        )
        menuitem "Enable CPU Load View"
        (
            IF !GROUP.EXIST(idle)
            (
                DIALOG.OK "Please set idle task first!"
                ENDDO
            )
            GROUP.MERGE "idle"
            GROUP.MERGE "other"
        )
    )
  )
  popup "&Help"
  (
    MENUITEM "[:manual]OS Awareness Manual PXROS" "HELP __RTOS_PXROS_"
  )
)


menu "task.lt"
(
  default
  menuitem "Display detailed"
  (
    LOCAL &magic
    &magic=address.offset(track.address())
    TASK.ListTask &magic
  )
  menuitem "Display task struct"
  (
    LOCAL &address
    &address=address.offset(track.address())
    Var.View %Open %String (PxTask_T)*&address ((PxTask_T)*&address).tcb
  )
  separator
  menuitem "Display Stack Frame"
  (
    LOCAL &magic
    &magic=address.offset(track.address())
    Var.Frame /Locals /Caller /Task &magic
  )
  menuitem "Display Registers"
  (
    LOCAL &magic
    &magic=address.offset(track.address())
    Register /Task &magic
  )
  menuitem "Switch Context"    "Register.TASK address.offset(track.address())"
  separator
  menuitem "Add to Stack Cov"  "TASK.STK.ADD address.offset(track.address())"
  menuitem "Rm from Stack Cov" "TASK.STK.RM  address.offset(track.address())"
  separator
  menuitem "Dump task entry"
  (
    LOCAL &address
    &address=track.address()
    Data.dump &address /Long /DIALOG
  )
)


menu "task.lx"
(
  default
  menuitem "Display detailed"
  (
    LOCAL &magic
    &magic=address.offset(track.address())
    TASK.ListmbX &magic
  )
  menuitem "Display mailbox struct"
  (
    LOCAL &address
    &address=address.offset(track.address())
    Var.View %Open %String (PxMbx_T)*&address
  )
  menuitem "Dump mailbox entry"
  (
    LOCAL &address
    &address=track.address()
    Data.dump &address /Long /DIALOG
  )
)


menu "task.lo.mc"
(
  default
  menuitem "Display mem class struct"
  (
    &address=address.offset(track.address())
    Var.View %m %s (PxMc_T)*&address
  )
  menuitem "Dump mem class entry"
  (
    &address=address.offset(track.address())
    Data.dump &address /l /dialog
  )
)

